; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define programfiles GetEnv('ProgramFiles')

[Components]
Name: python; Description: "Python 3.7.4"; Types: full
Name: examples; Description: "Example Code Listings"; Types: full
Name: PyQt; Description: "Python Qt GUI library"; Types: full
Name: pygame; Description: "pygame"; Types: full
Name: easygui; Description: "easygui"; Types: full
Name: NumericPython; Description: "Numeric Python"; Types: full


;[Tasks]
;Name: modifypath; Description: Add application directory to your system path; Flags: unchecked

[Code]

var
    Output: String;
    Path: String;
    Base: String;
    PythonDir: String;

function PipPathDialog(): String;
begin
    BrowseForFolder('Apologies, I should be able to figure out where you installed Python, but I struck out.  Can you find the directory named "Python37"?',
                    PythonDir,False);
    Output := PythonDir + '\Scripts\pip3.exe';
end;
function PipPath(Param: String): String;
begin
    Base := '\Python37\Scripts\pip3.exe';
    Path := GetEnv('ProgramFiles') + Base;
    if FileExists(Path) then begin
        Output := Path;
    end;
    Path := GetEnv('ProgramFiles(x86)') + Base;
    if FileExists(Path) then begin
        Output := Path;
    end;
    Path := GetEnv('ProgramW6432') + Base;
    if FileExists(Path) then begin
        Output := Path;
    end;
    if Output = '' then begin
       PipPathDialog();
    end;
    Result := Output;
end;
[Setup]
AppName=Hello World
AppVerName=Hello World 3.0
AppPublisher=Manning Inc
AppPublisherURL=http://www.manning.com/
AppSupportURL=http://www.helloworldbook.com/
DefaultDirName={pf}\Hello World
DefaultGroupName=Hello World
AllowNoIcons=yes
LicenseFile=.\license.txt
OutputDir=..\Output
OutputBaseFilename=hello_world_setup
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "packages\python-3.7.4-amd64.exe"; DestDir: "{tmp}"; Flags: ignoreversion; Components:  python
Source: "packages\PyQt5-5.13.0-5.13.0-cp35.cp36.cp37.cp38-none-win_amd64.whl"; DestDir: "{tmp}"; Components: PyQt
Source: "packages\easygui-0.98.1-py2.py3-none-any.whl"; DestDir: "{tmp}"; Components: easygui
Source: "packages\numpy-1.17.1-cp37-cp37m-win_amd64.whl"; DestDir: "{tmp}"; Components: NumericPython
Source: "packages\pygame-1.9.6-cp37-cp37m-win_amd64.whl"; DestDir: "{tmp}"; Components: pygame
Source: "..\sample-code\*"; DestDir: "{app}\examples"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: examples
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,Hello World}"; Filename: "http://www.helloworldbook.com/"
Name: "{group}\{cm:UninstallProgram,Hello World}"; Filename: "{uninstallexe}"

[Run]
Filename: "{tmp}\python-3.7.4-amd64.exe"; Parameters: "InstallAllUsers=1"; Components:  python
Filename: "{code:PipPath}"; Parameters: "install PyQt5 -f {tmp}\PyQt5-5.13.0-5.13.0-cp35.cp36.cp37.cp38-none-win_amd64.whl"; Components: PyQt
Filename: "{code:PipPath}"; Parameters: "install easygui -f {tmp}\easygui-0.98.1-py2.py3-none-any.whl"; Components: easygui
Filename: "{code:PipPath}"; Parameters: "install numpy -f {tmp}\numpy-1.17.1-cp37-cp37m-win_amd64.whl"; Components: NumericPython
Filename: "{code:PipPath}"; Parameters: "install pygame -f {tmp}\pygame-1.9.6-cp37-cp37m-win_amd64.whl"; Components: pygame


